#pragma version 4
// int 1

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Identification
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

global GroupSize
int 3
==
bnz branch_setup

global GroupSize
int 2 // central optout + unlock shares
==
bz not_group_size_2

gtxn 0 TypeEnum 
int appl
==
gtxn 0 NumAppArgs
int 1
==
&&
bz not_unlock
gtxn 0 ApplicationArgs 0
byte b64 dW5sb2Nr // unlock
==
gtxn 1 TypeEnum // unlock shares
int axfer
==
&&
bnz branch_unlock
not_unlock:

b branch_harvest

not_group_size_2:

b failure

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Handling
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

branch_setup:
// min balance funding
gtxn 0 TypeEnum
int pay
==
assert
// escrow optin to capi asset 
gtxn 1 TypeEnum
int axfer
==
assert
gtxn 1 XferAsset
int {capi_asset_id}
==
assert
// escrow optin to funds asset
gtxn 2 TypeEnum
int axfer
==
assert
gtxn 2 XferAsset
int {funds_asset_id}
==
assert
// TODO is it needed / possible to check that txs are sent/received by "this escrow"?

b success

branch_harvest:
gtxn 0 TypeEnum // app call
int appl
==
assert
int NoOp 
gtxn 0 OnCompletion
==
assert
gtxn 0 ApplicationID
int {app_id}
==
assert
gtxn 1 TypeEnum // harvest
int axfer
==
assert
gtxn 1 XferAsset
int {funds_asset_id}
==
assert
// the app caller is the dividend receiver
gtxn 0 Sender
gtxn 1 AssetReceiver
==
assert

b success

branch_unlock:
gtxn 0 OnCompletion // app opt out
int CloseOut 
==
assert
gtxn 0 ApplicationID
int {app_id}
==
assert
gtxn 1 TypeEnum // unlock
int axfer
==
assert
gtxn 1 XferAsset
int {capi_asset_id}
==
assert
// the app caller is the dividend receiver
// TODO do we need checks duplicated with the app if we're checking that we're calling the app too
gtxn 1 AssetReceiver
gtxn 0 Sender
==
assert
b success

success:
int 1
return

failure:
int 0
return
